# Copyright (c) 2003-2020, Ansys, Inc. All rights reserved.  

# Unauthorized use, distribution, or duplication is prohibited.  
# This product is subject to U.S. laws governing export and re-export.  

# For full Legal Notice, see documentation.
# S and Vpi QA for balanced MZMs

# Test circuit 1a: Vpi
if (exist("source_directory")){
    if (source_directory!=filedirectory(filedirectory(which(currentscriptname)))){
        clear;
        source_directory = filedirectory(filedirectory(which(currentscriptname)));
        foundry_directory = filedirectory(filedirectory(which(source_directory)));
        cd(filedirectory(which(currentscriptname)));
        feval("/aux_scripts/mach_zehnder_modulator_2x2_init_qa.lsf");
    }
}else{
        clear;
        source_directory = filedirectory(filedirectory(which(currentscriptname)));
        foundry_directory = filedirectory(filedirectory(which(source_directory)));
        cd(filedirectory(which(currentscriptname)));
        feval("/aux_scripts/mach_zehnder_modulator_2x2_init_qa.lsf");      
}
err=error_count;

if (!unbalanced_MZM){
    if(mzm_electrical) {
        V_test = linspace(Vpi_electrical_voltage1, Vpi_electrical_voltage2, V_num);
        V_num_original = V_num;
        
        length_scale_factor = 1;
        V_sign = (Vpi_electrical_voltage2-Vpi_electrical_voltage1)/abs(Vpi_electrical_voltage2-Vpi_electrical_voltage1);
            
        if(abs(Vpi_electrical) >= abs(Vpi_electrical_voltage2 -  Vpi_electrical_voltage1)){
            # If the Vpi for the device is larger than (or equal to) the allowed bias range 
            # then make the length larger for QA purpose and add extra bias points at the end for a finer voltage grid near Vpi
            length_scale_factor = abs(Vpi_electrical) / abs(Vpi_electrical_voltage2 -  Vpi_electrical_voltage1) * 1.01;
            V_fine_range = min([abs(V_test(V_num)-V_test(V_num-1)), 0.04/length_scale_factor*abs(Vpi_electrical_voltage2-Vpi_electrical_voltage1)]);
            V_test = [V_test(1:V_num-1);linspace(Vpi_electrical_voltage2-V_sign*V_fine_range,Vpi_electrical_voltage2,5)];
            V_num = V_num+4;
        }
        else{
            # If Vpi is smaller than the bias range then add extra points near Vpi to increase accuracy of finding the minima
            V_fine_range = min([abs(V_test(V_num)-V_test(V_num-1)), 0.04*abs(Vpi_electrical_voltage2-Vpi_electrical_voltage1)]);
            V_near_Vpi = find(V_test<=V_sign*abs(Vpi_electrical)+V_fine_range/2 & V_test>=V_sign*abs(Vpi_electrical)-V_fine_range/2);
            if(V_near_Vpi == 0){
                Vtemp1 = find(V_test>-abs(Vpi_electrical));
                Vtemp2 = find(V_test<-abs(Vpi_electrical));
                V_test = [V_test(1:Vtemp1(end)) ; linspace(V_sign*abs(Vpi_electrical)-V_sign*V_fine_range/2,V_sign*abs(Vpi_electrical)+V_sign*V_fine_range/2,5) ; V_test(Vtemp2(1):end)];
                V_num = V_num+5;
            }
            else{
                V_test = [V_test(1:V_near_Vpi-1) ; linspace(V_sign*abs(Vpi_electrical)-V_sign*V_fine_range/2,V_sign*abs(Vpi_electrical)+V_sign*V_fine_range/2,5) ; V_test(V_near_Vpi+1:end)];
                V_num = V_num+4;
            }
        }     
    }
    new;
    groupscope("::Root Element"); #return to root
    set("sample rate", sample_rate);


    fspan=sample_rate;
    if(mzm_electrical) {
    addelement("Optical Network Analyzer");
    ONA1_name = get("name");
    set("x position", -390);
    set("y position", -115);
    set("input parameter", "center and range");
    set("plot kind", "wavelength");
    set("center frequency", c/wavelength_data);
    set("frequency range", fspan);
    set("number of points", 3);
    set("orthogonal identifier", mode_data{1}.ID);
    set("number of input ports", 2*V_num);
    set("peak analysis","center");

    addelement("Optical Splitter");
    Split1_name = get("name");
    set("x position", -200);
    set("y position", 100);
    set("number of ports", V_num); ##
    set("split ratio","none"); ##

    addelement("DC Source");
    DC1_name = get("name");
    set("x position", -140);
    set("y position", -185);
    set("amplitude",0);

    connect(ONA1_name,"output",Split1_name,"input");

    #V_zero = find(V_test == 0);
    DUT1_name_cell = cell(V_num);
    DC2_name_cell = cell(V_num);    

    for (DUT1_num = 1:V_num){
        addelement(design_kit_name + model);
        if(exist("container_selection_code")) { eval(container_selection_code); }
        DUT1_name_cell{DUT1_num} = get("name");
        set("x position", 0);
        set("y position", 200*(DUT1_num-1));

        # If the Vpi for the device is larger than (or equal to) the allowed bias range 
        # then make the length larger for QA purpose
        if(abs(Vpi_electrical) >= abs(Vpi_electrical_voltage2 -  Vpi_electrical_voltage1)){
            set(wg_length.name,wg_length.default * length_scale_factor);
        }

        addelement("DC Source");
        DC2_name_cell{DUT1_num} = get("name");
        set("x position", 200);
        set("y position", 200*(DUT1_num-1));
        if(bias_node=="anode"){set("amplitude",V_test(DUT1_num));}
        if(bias_node=="cathode"){set("amplitude",-V_test(DUT1_num));}

        connect(Split1_name,"output "+num2str(DUT1_num),DUT1_name_cell{DUT1_num},ports{opt_1_RELAY}.name);

        connect(ONA1_name,"input "+num2str(DUT1_num),DUT1_name_cell{DUT1_num},ports{opt_3_RELAY}.name);
        connect(ONA1_name,"input "+num2str(V_num+DUT1_num),DUT1_name_cell{DUT1_num},ports{opt_4_RELAY}.name);

        try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_th_1_RELAY}.name);}catch(blank);
        try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_th_2_RELAY}.name);}catch(blank);
        try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_th_3_RELAY}.name);}catch(blank);
        try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_th_4_RELAY}.name);}catch(blank);
        try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_an_2_RELAY}.name);}catch(blank);
        try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_cat_2_RELAY}.name);}catch(blank);
        if(bias_node=="anode"){
            try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_cat_1_RELAY}.name);}catch(blank);
            try{connect(DC2_name_cell{DUT1_num},"output",DUT1_name_cell{DUT1_num},ports{ele_an_1_RELAY}.name);}catch(blank);
        }
        if(bias_node=="cathode"){
            try{connect(DC1_name,"output",DUT1_name_cell{DUT1_num},ports{ele_an_1_RELAY}.name);}catch(blank);
            try{connect(DC2_name_cell{DUT1_num},"output",DUT1_name_cell{DUT1_num},ports{ele_cat_1_RELAY}.name);}catch(blank);
        }
    }
}
# Test circuit 1b: S13 and S14

    addelement("Optical Network Analyzer");
    set("name","ONA_2");
    ONA2_name = get("name");
    set("x position", -990);
    set("y position", -115);
    set("input parameter", "center and range");
    set("plot kind", "wavelength");
    set("center frequency", c/wavelength_data);
    set("frequency range", fspan);
    set("number of points", 3);
    set("orthogonal identifier", mode_data{1}.ID);
    set("number of input ports", 2);
    set("peak analysis","center");

    addelement("DC Source");
    DC3_name = get("name");
    set("x position", -740);
    set("y position", -185);
    set("amplitude",0);

    addelement(design_kit_name + model);
    if(exist("container_selection_code")) { eval(container_selection_code); }
    DUT2_name = get("name");
    set("x position", -600);
    set("y position", 0);

    connect(ONA2_name,"input 1",DUT2_name,ports{opt_3_RELAY}.name);
    connect(ONA2_name,"input 2",DUT2_name,ports{opt_4_RELAY}.name);
    connect(ONA2_name,"output",DUT2_name,ports{opt_1_RELAY}.name);

    try{connect(DC3_name,"output",DUT2_name,ports{ele_th_1_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_th_2_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_th_3_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_th_4_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_an_1_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_an_2_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_cat_2_RELAY}.name);}catch(blank);
    try{connect(DC3_name,"output",DUT2_name,ports{ele_cat_1_RELAY}.name);}catch(blank);

    if(exist("stat_parameters")){
        selectpartial(prefix);
        set("perturb_stat_params",0);  # flag to set all stat_prameters to zero for comparison with target values
    }
    run;

    

    # Test result 1: S13, S14, Vpi

if(mzm_electrical){
    
    S13_dB = matrix(V_num,3);
    S14_dB = matrix(V_num,3);
    S13_test = matrix(V_num);
    S14_test = matrix(V_num);    

    for (DUT1_num = 1:V_num){
        gain_result = getresult(ONA1_name,"input "+num2str(DUT1_num)+"/mode 1/gain");
        lambda = gain_result.wavelength;
        gain_result = gain_result.getattribute("TE gain (dB)");
        S13_dB(DUT1_num,:) = gain_result;
        gain_result = getresult(ONA1_name,"input "+num2str(V_num+DUT1_num)+"/mode 1/gain");
        gain_result = gain_result.getattribute("TE gain (dB)");
        S14_dB(DUT1_num,:) = gain_result;
        T_result = getresult(ONA1_name,"input "+num2str(DUT1_num)+"/mode 1/transmission");
        T_result = T_result.getattribute("TE transmission");
        S13_test(DUT1_num) = T_result(2);
        T_result = getresult(ONA1_name,"input "+num2str(V_num+DUT1_num)+"/mode 1/transmission");
        T_result = T_result.getattribute("TE transmission");
        S14_test(DUT1_num) = T_result(2);
    }
    
    # find the minima in IL to calculate Vpi
    temp = findpeaks(abs(S13_test)^2,10);
    Vpi_test = abs(V_test(temp(1)))*length_scale_factor;
}

gain_result = getresult(ONA2_name,"input 1/mode 1/gain");
lambda = gain_result.wavelength;
S13_dB = gain_result.getattribute("TE gain (dB)");
S13_dB_zerobias = pinch(S13_dB,1,2);
gain_result = getresult(ONA2_name,"input 2/mode 1/gain");
lambda = gain_result.wavelength;
S14_dB = gain_result.getattribute("TE gain (dB)");
S14_dB_zerobias = pinch(S14_dB,1,2);

if (create_plots){
    title = model+"_Transmission";
    if(mzm_electrical){
        plot(V_test,abs(S13_test)^2,abs(S14_test)^2, "Bias (V)", "Transmission", title);
        legend("|S13|^2","|S14|^2");
        
        if(length_scale_factor>1){
            setplot("title",wg_length.name+" scaled by a factor of "+num2str(length_scale_factor));
        }
    }
    title = model+"_Transmission (dB)";
    
    plot(lambda*1e9,S13_dB,S14_dB, "Wavelength (nm)", "Transmission (dB)", title);
    legend("|S13|^2","|S14|^2");
}

msg = "S13 is "+num2str(S13_dB_zerobias)+" dB and it should be "+num2str(S13)+" dB";
change = abs(S13_dB_zerobias-S13)/abs(S13);
pass = change < S13_tolerance;
if(!pass) {
    ?msg;
    error_count = error_count+1;
}
assert(msg,pass);
if(break_at_failure & !pass) { break; }

msg = "S14 is "+num2str(S14_dB_zerobias)+" dB and it should be "+num2str(S14)+" dB";
change = abs(S14_dB_zerobias-S14)/abs(S14);
pass = change < S14_tolerance;
if(!pass) {
    ?msg;
    error_count = error_count+1;
}
assert(msg,pass);
if(break_at_failure & !pass) { break; }

if(mzm_electrical) {
    msg = "Vpi is "+num2str(Vpi_test)+" V and it should be "+num2str(abs(Vpi_electrical))+" V";
    change = abs(Vpi_test-Vpi_electrical)/abs(Vpi_electrical);
    pass = change < Vpi_electrical_tolerance;
    if(!pass) {
        ?msg;
        error_count = error_count+1;
    }
    assert(msg,pass);
    if(break_at_failure & !pass) { break; }
    V_num = V_num_original;  # reset V_num    
}
    if (err==error_count){
        ?filebasename(currentscriptname)+" test passed.";
    }
}else{
    ?"Test-bench is not applicable to this element. "+ filebasename(currentscriptname)+" did not run!";
}